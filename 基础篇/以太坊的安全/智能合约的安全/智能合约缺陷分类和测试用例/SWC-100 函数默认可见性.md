## SWC-100 函数默认可见性
原文链接：https://swcregistry.io/docs/SWC-100

### 关系
[CWE-710: 没有正确的遵守编码规则](https://cwe.mitre.org/data/definitions/710.html)

### 描述
默认情况下，没有指定函数可见性类型的函数是公共的。如果开发人员忘记设置可见性，并且恶意用户能够进行未经授权或无意的状态更改，就会导致漏洞。

### 修复
函数可以指定为外部的、公共的、内部的或私有的。建议有意识地决定哪种可见性类型适合某个函数。这可以极大地减少合同系统的攻击面。

### 参考
+ 以太坊智能合约最佳实践——显式标记函数和状态变量的可见性
+ [SigmaPrime——可见性](https://github.com/sigp/solidity-security-blog#visibility)

### 合约例子
**visibility_not_set.sol**
```
/*
 * @source: https://github.com/sigp/solidity-security-blog#visibility
 * @author: SigmaPrime 
 * Modified by Gerhard Wagner
 */

pragma solidity ^0.4.24;

contract HashForEther {

    function withdrawWinnings() {
        // Winner if the last 8 hex characters of the address are 0. 
        require(uint32(msg.sender) == 0);
        _sendWinnings();
     }

     function _sendWinnings() {
         msg.sender.transfer(this.balance);
     }
}
```

**visibility_not_set.yaml**
```
description: Default function visibility
issues:
- id: SWC-100
  count: 2
  locations:
  - bytecode_offsets: {}
    line_numbers:
      visibility_not_set.sol: [11]
  - bytecode_offsets: {}
    line_numbers:
      visibility_not_set.sol: [17]
```

**visibility_not_set_fixed.sol**
```
/*
 * @source: https://github.com/sigp/solidity-security-blog#visibility
 * @author: SigmaPrime
 * Modified by Gerhard Wagner
 */

pragma solidity ^0.4.24;

contract HashForEther {

    function withdrawWinnings() public {
        // Winner if the last 8 hex characters of the address are 0.
        require(uint32(msg.sender) == 0);
        _sendWinnings();
     }

     function _sendWinnings() internal{
         msg.sender.transfer(this.balance);
     }
}
```

**visibility_not_set_fixed.yaml**
```
description: Default function visibility
issues:
- id: SWC-100
  count: 0
  locations: []
```
